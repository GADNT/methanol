buildscript {
  ext.versions = [
      'junit'        : '5.5.2',
      'junitPlatform': '1.3.1',
      'modulePlugin' : '1.6.0'
  ]

  ext.deps = [
      'junitApi'             : "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
      'junitEngine'          : "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
      'junitPlatformLauncher': "org.junit.platform:junit-platform-launcher:${versions.junitPlatform}",
      'modulePlugin'         : "org.javamodularity:moduleplugin:${versions.modulePlugin}"
  ]

  repositories {
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.modulePlugin
  }
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'org.javamodularity.moduleplugin'

  group = 'com.github.mizosoft.methanol'
  version = '1.0-SNAPSHOT'

  sourceCompatibility = JavaVersion.VERSION_11

  repositories {
    jcenter()
  }

  dependencies {
    testImplementation deps.junitApi
    testRuntimeOnly deps.junitEngine
    // Gradle already has the launcher, but the explicit dependency avoids a weird
    // IllegalAccessError when org.junit.platform.launcher is implied as unnamed which
    // prevents tests from running in the module path
    testRuntimeOnly deps.junitPlatformLauncher
  }

  tasks.withType(Test) {
    testLogging {
      events 'skipped', 'failed'
      showStandardStreams = true
    }
  }

  test {
    useJUnitPlatform()
  }
}
