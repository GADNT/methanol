buildscript {
  ext.versions = [
      'junit'           : '5.5.2',
      'junitPlatform'   : '1.3.1',
      'checkerFramework': '3.0.0',
      'modulePlugin'    : '1.6.0',
      'reactiveStreams' : '1.0.3'
  ]

  ext.deps = [
      'junitApi'              : "org.junit.jupiter:junit-jupiter-api:${versions.junit}",
      'junitEngine'           : "org.junit.jupiter:junit-jupiter-engine:${versions.junit}",
      'junitPlatformLauncher' : "org.junit.platform:junit-platform-launcher:${versions.junitPlatform}",
      'checkerFramework'      : "org.checkerframework:checker:${versions.checkerFramework}",
      'checkerFrameworkQual'  : "org.checkerframework:checker-qual:${versions.checkerFramework}",
      'modulePlugin'          : "org.javamodularity:moduleplugin:${versions.modulePlugin}",
      'reactiveStreamsTckFlow': "org.reactivestreams:reactive-streams-tck-flow:${versions.reactiveStreams}"
  ]

  repositories {
    gradlePluginPortal()
  }

  dependencies {
    classpath deps.modulePlugin
  }
}

plugins {
  id 'org.checkerframework' version '0.4.9' apply false
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'org.checkerframework'
  apply plugin: 'org.javamodularity.moduleplugin'

  group = 'com.github.mizosoft.methanol'
  version = '1.0-SNAPSHOT'

  sourceCompatibility = JavaVersion.VERSION_11

  repositories {
    jcenter()
  }

  dependencies {
    implementation deps.checkerFrameworkQual
    checkerFramework deps.checkerFramework

    testImplementation deps.junitApi
    testRuntimeOnly deps.junitEngine
    // Gradle already has the launcher, but the explicit dependency avoids a weird
    // IllegalAccessError when org.junit.platform.launcher is implied as unnamed which
    // prevents tests from running in the module path
    testRuntimeOnly deps.junitPlatformLauncher
    testImplementation deps.reactiveStreamsTckFlow
  }

  checkerFramework {
    checkers = [
        // 'org.checkerframework.checker.nullness.NullnessChecker'
    ]
  }

  tasks.withType(Test) {
    exclude("**/testing/***")
    testLogging {
      events = ['skipped', 'failed']
      showStandardStreams = true
      exceptionFormat = "full"
    }
  }

  test {
    useJUnitPlatform()
  }
}
