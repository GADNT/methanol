dependencies {
  testImplementation deps.reactiveStreamsTckFlow
  testImplementation project(':methanol-testutils')
}

compileJava {
  // methanol exports a package to methanol.adapter.jackson but the compiler won't be able
  // to resolve methanol.adapter.jackson as it's not a dependency so it issues a warning
  options.compilerArgs += '-Xlint:-module'
}

task testNG(type: Test) {
  useTestNG()
  include '**/tck/**'
  shouldRunAfter test
  scanForTestClasses = false
}

test {
  exclude '**/tck/**'
  // run from classpath to use META-INF/services which doesn't work from module path
  moduleOptions {
    runOnClasspath = true
  }
}

// compileTestJava still compiles from the module path
addReads(compileTestJava, 'methanol.testutils')
